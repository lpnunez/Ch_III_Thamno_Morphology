################################################################################
#
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   state-dependent Brownian-motion model
#
#
# authors: Michael R. May
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
character <- 1

### Read in the tree
T <- readTrees("Analyses_Scripts/RevBayes_Analyses/Data/Thamnophiini_UCE_51_Taxa.nex")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

### Read in the continuous-character data
cont <- readContinuousCharacterData("Analyses_Scripts/RevBayes_Analyses/Data/Skull_PACA.nex")
#cont.excludeAll()
#cont.includeCharacter(character)
nchar <- cont.nchar()

### Read in the discrete-character data
disc <- readDiscreteCharacterData("Analyses_Scripts/RevBayes_Analyses/Data/Habitat.nex")
num_disc_states <- disc.getStateDescriptions().size()

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix
Q <- fnJC(num_disc_states)

# make the transition rate parameter
lambda ~ dnLoguniform(1e-3, 1)
moves.append( mvScale(lambda, weight=1.0) )

# make the data-augmented CTCM model
X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)

# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=20.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=20.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)


##########################
# Specify the rate model #
##########################

# specify the average rate
beta_root ~ dnLoguniform(1e-3, 1)
moves.append( mvScale(beta_root, weight=1.0) )

# specify the prior on the number of rate shifts
expected_number_of_shifts <- 5
rate_shift_probability    <- expected_number_of_shifts / nbranches

# specify the prior on the magnitude of rate shifts
sd = 0.578
rate_shift_distribution = dnLognormal(-sd^2/2, sd)

# specify the branch-specific rates
for(i in nbranches:1) {

    # draw the rate multiplier from a mixture distribution
    branch_rate_multiplier[i] ~ dnReversibleJumpMixture(1, rate_shift_distribution, Probability(1 - rate_shift_probability) )

    # compute the rate for the branch
    if ( tree.isRoot( tree.parent(i) ) ) {
       background_rates[i] := beta_root * branch_rate_multiplier[i]
    } else {
       background_rates[i] := background_rates[tree.parent(i)] * branch_rate_multiplier[i]
    }

    # keep track of whether the branch has a rate shift
    branch_rate_shift[i] := ifelse( branch_rate_multiplier[i] == 1, 0, 1 )

    # use reversible-jump to move between models with and without
    # shifts on the branch
    moves.append( mvRJSwitch(branch_rate_multiplier[i], weight=1) )

    # include proposals on the rate mutliplier (when it is not 1)
    moves.append( mvScale(branch_rate_multiplier[i], weight=1) )
}

# keep track of the number of rate shifts
num_rate_changes := sum( branch_rate_shift )

# specify the relative state-dependent rates (with sum 1)
# concentration <- 1.0
# proportional_zeta ~ dnDirichlet( rep(concentration, num_disc_states) )
# moves.append( mvBetaSimplex(proportional_zeta, weight=1.0) )

# specify the relative state-dependent rates (with sum 1) and calculate posterior probability that these rates are state-dependent #

concentration <- 1.0
proportional_zeta ~ dnReversibleJumpMixture( simplex(rep(1,num_disc_states)), dnDirichlet( rep(concentration, num_disc_states) ), p=0.5 )
moves.append( mvRJSwitch(proportional_zeta, weight=10.0) )
moves.append( mvBetaSimplex(proportional_zeta, weight=1.0) )
is_state_dependent := ifelse( proportional_zeta == simplex(rep(1,num_disc_states)), 0.0, 1.0)

# compute the state dependent rates (with mean 1)
zeta := proportional_zeta * num_disc_states

# compute the state-dependent branch rates
for(i in 1:nbranches) {
    state_branch_rate[i] := sum(abs(X.relativeTimeInStates(i,1)) * abs(zeta))
}

# compute the overall branch rates (including the background rates)
branch_rates := state_branch_rate * background_rates

alpha <- 1.0
proportional_rates ~ dnDirichlet( rep(alpha, nchar) )
relative_rates := proportional_rates * nchar
moves.append( mvBetaSimplex(proportional_rates, weight=2.0) )

eta <- 1.0
P ~ dnLKJPartial( eta, nchar )

moves.append( mvCorrelationMatrixRandomWalk(P, weight=3.0) )
moves.append( mvCorrelationMatrixSingleElementBeta(P, weight=5.0) )

R := fnPartialToCorr(P)

correlations := R.upperTriangle()

V := fnDecompVarCovar( relative_rates^0.5, R )

##########################
# Specify the BM process #
##########################

#Y ~ dnPhyloBrownianREML(tree, branchRates=branch_rates^0.5)
#Y.clamp(cont)

Y ~ dnPhyloMultivariateBrownianREML(tree, branchRates=branch_rates^0.5, rateMatrix=V)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(zeta)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="Results/Habitat/Skull/relaxed_state_dependent_hypo_test.log", printgen=10) )
monitors.append( mnScreen(printgen=1000, zeta, num_rate_changes, total_num_changes) )
monitors.append( mnExtNewick(filename="Results/Habitat/Skull/relaxed_state_dependent_hypo_test.trees", isNodeParameter=TRUE, printgen=10, separator=TAB, tree=tree, state_branch_rate, background_rates, branch_rates) )



################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="mixed")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100)
mymcmc.run(generations=1000000)

## summarize the branch rates
treetrace = readTreeTrace("Results/Habitat/Skull/relaxed_state_dependent_hypo_test.trees")
map_tree = mapTree(treetrace,"Results/Habitat/Skull/relaxed_state_dependent_hypo_test_MAP.tre")


## quit ##
q()
